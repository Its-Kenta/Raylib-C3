module rl::easings;
import libc;

//----------------------------------------------------------------------------------
// Module Functions Definition - Utils math
//----------------------------------------------------------------------------------

// Linear Easing functions
fn float easeLinearNone(float t, float b, float c, float d) { return (c*t/d + b); }                            // Ease: Linear
fn float easeLinearIn(float t, float b, float c, float d) { return (c*t/d + b); }                              // Ease: Linear In
fn float easeLinearOut(float t, float b, float c, float d) { return (c*t/d + b); }                             // Ease: Linear Out
fn float easeLinearInOut(float t, float b, float c, float d) { return (c*t/d + b); }                           // Ease: Linear In Out

// Sine Easing functions
fn float easeSineIn(float t, float b, float c, float d) { return (-c*$$cos(t/d*(rl::PI/2.0f)) + c + b); }            // Ease: Sine In
fn float easeSineOut(float t, float b, float c, float d) { return (c*$$sin(t/d*(rl::PI/2.0f)) + b); }                // Ease: Sine Out
fn float easeSineInOut(float t, float b, float c, float d) { return (-c/2.0f*($$cos(rl::PI*t/d) - 1.0f) + b); }      // Ease: Sine In Out

// Circular Easing functions
fn float easeCircIn(float t, float b, float c, float d) { t /= d; return (-c*($$sqrt(1.0f - t*t) - 1.0f) + b); } // Ease: Circular In
fn float easeCircOut(float t, float b, float c, float d) { t = t/d - 1.0f; return (c*$$sqrt(1.0f - t*t) + b); }  // Ease: Circular Out
fn float easeCircInOut(float t, float b, float c, float d)                                                      // Ease: Circular In Out
{
    if ((t/=d/2.0f) < 1.0f) return (-c/2.0f*($$sqrt(1.0f - t*t) - 1.0f) + b);
    t -= 2.0f; return (c/2.0f*($$sqrt(1.0f - t*t) + 1.0f) + b);
}

// Cubic Easing functions
fn float easeCubicIn(float t, float b, float c, float d) { t /= d; return (c*t*t*t + b); }                      // Ease: Cubic In
fn float easeCubicOut(float t, float b, float c, float d) { t = t/d - 1.0f; return (c*(t*t*t + 1.0f) + b); }    // Ease: Cubic Out
fn float easeCubicInOut(float t, float b, float c, float d)                                                     // Ease: Cubic In Out
{
    if ((t/=d/2.0f) < 1.0f) return (c/2.0f*t*t*t + b);
    t -= 2.0f; return (c/2.0f*(t*t*t + 2.0f) + b);
}

// Quadratic Easing functions
fn float easeQuadIn(float t, float b, float c, float d) { t /= d; return (c*t*t + b); }                         // Ease: Quadratic In
fn float easeQuadOut(float t, float b, float c, float d) { t /= d; return (-c*t*(t - 2.0f) + b); }              // Ease: Quadratic Out
fn float easeQuadInOut(float t, float b, float c, float d)                                                      // Ease: Quadratic In Out
{
    if ((t/=d/2) < 1) return (((c/2)*(t*t)) + b);
    return (-c/2.0f*(((t - 1.0f)*(t - 3.0f)) - 1.0f) + b);
}

// Exponential Easing functions
fn float easeExpoIn(float t, float b, float c, float d) { return (t == 0.0f) ? b : (c*$$pow(2.0f, 10.0f*(t/d - 1.0f)) + b); }       // Ease: Exponential In
fn float easeExpoOut(float t, float b, float c, float d) { return (t == d) ? (b + c) : (c*(-$$pow(2.0f, -10.0f*t/d) + 1.0f) + b); } // Ease: Exponential Out
fn float easeExpoInOut(float t, float b, float c, float d)                                                                         // Ease: Exponential In Out
{
    if (t == 0.0f) return b;
    if (t == d) return (b + c);
    if ((t/=d/2.0f) < 1.0f) return (c/2.0f*$$pow(2.0f, 10.0f*(t - 1.0f)) + b);

    return (c/2.0f*(-$$pow(2.0f, -10.0f*(t - 1.0f)) + 2.0f) + b);
}

// Back Easing functions
fn float easeBackIn(float t, float b, float c, float d) // Ease: Back In
{
    float s = 1.70158f;
    float postFix = t/=d;
    return (c*(postFix)*t*((s + 1.0f)*t - s) + b);
}

fn float easeBackOut(float t, float b, float c, float d) // Ease: Back Out
{
    float s = 1.70158f;
    t = t/d - 1.0f;
    return (c*(t*t*((s + 1.0f)*t + s) + 1.0f) + b);
}

fn float easeBackInOut(float t, float b, float c, float d) // Ease: Back In Out
{
    float s = 1.70158f;
    if ((t/=d/2.0f) < 1.0f)
    {
        s *= 1.525f;
        return (c/2.0f*(t*t*((s + 1.0f)*t - s)) + b);
    }

    float postFix = t-=2.0f;
    s *= 1.525f;
    return (c/2.0f*((postFix)*t*((s + 1.0f)*t + s) + 2.0f) + b);
}

// Bounce Easing functions
fn float easeBounceOut(float t, float b, float c, float d) // Ease: Bounce Out
{
    if ((t/=d) < (1.0f/2.75f))
    {
        return (c*(7.5625f*t*t) + b);
    }
    else if (t < (2.0f/2.75f))
    {
        float postFix = t-=(1.5f/2.75f);
        return (c*(7.5625f*(postFix)*t + 0.75f) + b);
    }
    else if (t < (2.5/2.75))
    {
        float postFix = t-=(2.25f/2.75f);
        return (c*(7.5625f*(postFix)*t + 0.9375f) + b);
    }
    else
    {
        float postFix = t-=(2.625f/2.75f);
        return (c*(7.5625f*(postFix)*t + 0.984375f) + b);
    }
}

fn float easeBounceIn(float t, float b, float c, float d) { return (c - easeBounceOut(d - t, 0.0f, c, d) + b); } // Ease: Bounce In
fn float easeBounceInOut(float t, float b, float c, float d) // Ease: Bounce In Out
{
    if (t < d/2.0f) { return (easeBounceIn(t*2.0f, 0.0f, c, d)*0.5f + b); }
    else { return (easeBounceOut(t*2.0f - d, 0.0f, c, d)*0.5f + c*0.5f + b); }
}

// Elastic Easing functions
fn float easeElasticIn(float t, float b, float c, float d) // Ease: Elastic In
{
    if (t == 0.0f) return b;
    if ((t/=d) == 1.0f) return (b + c);

    float p = d*0.3f;
    float a = c;
    float s = p/4.0f;
    float postFix = a*$$pow(2.0f, 10.0f*(t-=1.0f));

    return (-(postFix*$$sin((t*d-s)*(2.0f*rl::PI)/p )) + b);
}

fn float easeElasticOut(float t, float b, float c, float d) // Ease: Elastic Out
{
    if (t == 0.0f) return b;
    if ((t/=d) == 1.0f) return (b + c);

    float p = d*0.3f;
    float a = c;
    float s = p/4.0f;

    return (a*$$pow(2.0f,-10.0f*t)*$$sin((t*d-s)*(2.0f*rl::PI)/p) + c + b);
}

fn float easeElasticInOut(float t, float b, float c, float d) // Ease: Elastic In Out
{
    if (t == 0.0f) return b;
    if ((t/=d/2.0f) == 2.0f) return (b + c);

    float p = d*(0.3f*1.5f);
    float a = c;
    float s = p/4.0f;

    if (t < 1.0f)
    {
        float postFix = a*$$pow(2.0f, 10.0f*(t-=1.0f));
        return -0.5f*(postFix*$$sin((t*d-s)*(2.0f*rl::PI)/p)) + b;
    }

    float postFix = a*$$pow(2.0f, -10.0f*(t-=1.0f));

    return (postFix*$$sin((t*d-s)*(2.0f*rl::PI)/p)*0.5f + c + b);
}